Version "4.6.1"

class SMPBEventHandler : StaticEventHandler
{
	ThinkerIterator it;

	override void WorldTick()
	{
		if (!smpb_enable)
			return;

		int slowdownspeed = smpb_tics;
		float slowrange = smpb_range;

		if ((gametic % slowdownspeed) != 0)
			return;

		if (!it)
			it = ThinkerIterator.Create("Actor", Thinker.STAT_DEFAULT);
		else
			it.Reinit();

		actor mo;

		while (mo = Actor(it.Next()))
		{
			if (mo.isFrozen())
				break;

			bool inrange = false;
			for (int i = 0; i < MAXPLAYERS; i++)
			{
				if (players[i].mo && players[i].camera)
				{
					float dx = int(players[i].camera.Pos.X - mo.Pos.X);
					float dy = int(players[i].camera.Pos.Y - mo.Pos.Y);
					float qdist = dx * dx + dy * dy - slowrange * slowrange;
					if (qdist < 0)
						inrange = true;
				}
			}
			if (!(inrange) && !(mo.player) && (mo.Tics != -1))
			{
				mo.Tics += (slowdownspeed - 1);
				if (!(mo.bJustHit) && (smpb_distmoreaggro) && (mo.bIsMonster))
					mo.bJustHit = true;
			}
		}
	}
}


// party mode. let's actually test this thing!

class AlertEverything : CustomInventory
{

	states
	{
	Pickup:
		TNT1 A 0 {
			ThinkerIterator it;
			if (!it)
				it = ThinkerIterator.Create("Actor", Thinker.STAT_DEFAULT);
			else
				it.Reinit();

			actor mo;

			while (mo = Actor(it.Next()))
			{
				mo.LastHeard = Self;
				mo.bAmbush = false;
			}
			it.Destroy();
			it = null;
		}
	}
}
